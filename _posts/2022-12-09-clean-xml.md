---
layout: post
title: Format xml with XML::LibXML
published: yes
tags:
  - perl
  - XML::LibXML
  - format
---
Here are few notes for formatting XML files using [XML::LibXML][1]. Simple example for the different 
formatting styles using [toString][2] serializing function:

```perl
use v5.16;
use XML::LibXML;

my $data = '<tool><name>TPP1</name><version><major>1</major><minor>0</minor><revision>0</revision></version><installer>TPP1_setup.exe</installer><support_files/></tool>';
my $xml = XML::LibXML->new->load_xml(string => $data);

say $xml->toString(0);

# <?xml version="1.0"?>
# <tool><name>TPP1</name><version><major>1</major><minor>0</minor><revision>0</revision></version><installer>TPP1_setup.exe</installer><support_files/></tool>

say $xml->toString(1);

# <?xml version="1.0"?>
# <tool>
#   <name>TPP1</name>
#   <version>
#     <major>1</major>
#     <minor>0</minor>
#     <revision>0</revision>
#   </version>
#   <installer>TPP1_setup.exe</installer>
#   <support_files/>
# </tool>
```

But this can be a bit problematic if the XML contains some whitespace in the nodes. 
If the whitespace is only between the nodes, it helps to specify `no_blanks` [parser option][3] 
to `load_xml` method.

```perl
use v5.16;
use XML::LibXML;

my $data = <<END_XML;
                                    <nested_nodes>
                                        <nested_node>
                                        <configuration>A</configuration>
                                        <model>45</model>
                                        <added_node>
        <ID>
            <type>D</type>
            <serial>3</serial>
            <kVal>3</kVal>
        </ID>
    </added_node>
</nested_node>
                                    </nested_nodes>
END_XML

say XML::LibXML->load_xml(string => $data)->toString(1);

# <?xml version="1.0"?>
# <nested_nodes>
#                                         <nested_node>
#                                         <configuration>A</configuration>
#                                         <model>45</model>
#                                         <added_node>
#         <ID>
#             <type>D</type>
#             <serial>3</serial>
#             <kVal>3</kVal>
#         </ID>
#     </added_node>
# </nested_node>
#                                     </nested_nodes>

say XML::LibXML->load_xml(string => $data, { no_blanks => 1 })->toString(1);

# <?xml version="1.0"?>
# <nested_nodes>
#   <nested_node>
#     <configuration>A</configuration>
#     <model>45</model>
#     <added_node>
#       <ID>
#         <type>D</type>
#         <serial>3</serial>
#         <kVal>3</kVal>
#       </ID>
#     </added_node>
#   </nested_node>
# </nested_nodes>
```

If the whitespace is combined with actual values and it is not significant when adjacent
to tags, it is possible to trim it:

```perl
use v5.16;
use XML::LibXML;
use Text::Trim qw(trim);

my $root = XML::LibXML->load_xml(location => 'input.xml', { no_blanks => 1 })->documentElement;
for my $node ($root->findnodes('//text()')) {
    $node->setData(trim($node->getValue()));
}

say $root->toString(1);
```

That's about it for the formatting. Keep your XMLs clean.

[1]: https://metacpan.org/pod/XML::LibXML
[2]: https://metacpan.org/dist/XML-LibXML/view/lib/XML/LibXML/Document.pod#toString
[3]: https://metacpan.org/dist/XML-LibXML/view/lib/XML/LibXML/Parser.pod#PARSER-OPTIONS
